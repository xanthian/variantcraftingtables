plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' } // Required for Geckolib
    maven { url 'https://jitpack.io' }                // Required for Dawn API
    maven { url 'https://maven.terraformersmc.com/' } // Required for Mod Menu and Terraform APIs
    maven { url 'https://maven.shedaniel.me/' }       // Required for Cloth Config
    maven { url 'https://maven.ryanliptak.com/' }     // Required for AppleSkin
    maven { url 'https://maven.minecraftforge.net/' } // Required for TerraBlender
    maven { url "https://cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=variantcraftingtables"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}
dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'

    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Ad Astra
    modImplementation("curse.maven:adastra-635042:4643678")

    // BetterArcheology
    modImplementation("curse.maven:betterarcheology-835687:4600694")

    // Helions Sniffer
    modImplementation("curse.maven:hellionssiffer-871962:4591532")

    // Promenade
    modImplementation("curse.maven:promenade-399648:4611967")
        modRuntimeOnly("com.github.DawnTeamMC:DawnAPI:5.0.0")
        modRuntimeOnly("com.terraformersmc:biolith:1.0.0-alpha.7")

    // Tech Reborn
    modImplementation("TechReborn:TechReborn-1.20:5.8.3") {
        exclude group:"RebornCore" exclude group: "net.fabricmc.fabric-api" exclude group: "me.shedaniel" exclude group: "me.shedaniel.cloth" exclude group: "dev.architectury"
    }
    modRuntimeOnly("RebornCore:RebornCore-1.20:5.8.3")

}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
    }
}