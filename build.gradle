plugins {
    id 'fabric-loom' version '1.3-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven { url 'https://jitpack.io' }                // Required for Dawn API
    maven { url 'https://maven.terraformersmc.com/' } // Required for Mod Menu and Terraform APIs
    maven { url 'https://maven.shedaniel.me/' }       // Required for Cloth Config
    maven { url 'https://maven.ryanliptak.com/' }     // Required for AppleSkin
    maven { url 'https://maven.minecraftforge.net/' } // Required for TerraBlender
    maven { url "https://maven.resourcefulbees.com/repository/maven-public/" } // Required for Ad Astra
    maven { url "https://cursemaven.com/" }
    maven { url "https://api.modrinth.com/maven" }
}

loom {
    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=variantcraftingtables"

            runDir "build/datagen"
        }
    }
}

sourceSets {
    main {
        resources {
            srcDirs += [
                    'src/main/generated'
            ]
        }
    }
}
dependencies {
    implementation 'org.jetbrains:annotations:24.0.0'

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Ad Astra  - (issues with Botania)
    //modRuntimeOnly("curse.maven:adastra-635042:4643678") {transitive false}
    //modRuntimeOnly ("com.teamresourceful.resourcefullib:resourcefullib-fabric-1.20.1:2.1.3")
    //modRuntimeOnly ("curse.maven:resourcefulconfig-714059:4576456")

    // BetterArcheology
    modRuntimeOnly("curse.maven:betterarcheology-835687:4600694") {transitive false}

     // Promenade
    modRuntimeOnly("curse.maven:promenade-399648:4611967") {transitive false}
    modRuntimeOnly("com.github.DawnTeamMC:DawnAPI:5.0.0")
    modRuntimeOnly("com.terraformersmc:biolith:1.0.0-alpha.7")

    // Snifferplus - (Datagen issues)
    //modImplementation("curse.maven:hellionssiffer-871962:4591532")

    // Tech Reborn
    modRuntimeOnly("TechReborn:TechReborn-1.20:5.8.3") {transitive false }
    modRuntimeOnly("RebornCore:RebornCore-1.20:5.8.3")

    // Vinery
    modRuntimeOnly("curse.maven:vinery-704465:4643442") {transitive false}
    modRuntimeOnly "maven.modrinth:do-api:1.2.4-fabric"
    modApi( "curse.maven:arch-419699:4633047") { exclude group: 'net.fabricmc', module: 'fabric-loader' }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
    }
}